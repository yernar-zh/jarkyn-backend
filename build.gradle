import org.yaml.snakeyaml.Yaml

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.yaml:snakeyaml:2.2'
        classpath 'org.flywaydb:flyway-database-postgresql:12.0.0'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.3'
    id 'io.spring.dependency-management' version '1.1.6'
    id "org.flywaydb.flyway" version "12.0.0"
}

group = 'kz.jarkyn'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(22)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Web
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.apache.commons:commons-lang3:3.18.0'

    // Security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'io.jsonwebtoken:jjwt-api:0.12.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.5'

    // Cloud
    implementation 'software.amazon.awssdk:s3:2.31.23'

    // Database
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.flywaydb:flyway-core'
    runtimeOnly 'org.flywaydb:flyway-database-postgresql'
    implementation 'org.postgresql:postgresql'
    annotationProcessor 'org.hibernate:hibernate-jpamodelgen:6.2.5.Final'

    // Model and Databind
    implementation 'org.immutables:value:2.8.2'
    annotationProcessor 'org.immutables:value:2.8.2'
    implementation 'org.mapstruct:mapstruct:1.4.2.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'

    // Image processioning
    implementation 'net.coobird:thumbnailator:0.4.20'

    //Excel
    implementation 'org.jxls:jxls-poi:2.14.0'

    // Message Broker
    implementation 'org.springframework.boot:spring-boot-starter-amqp'

    //Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'com.opentable.components:otj-pg-embedded:1.1.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}


compileJava {
    options.compilerArgs += [
            '-Amapstruct.defaultComponentModel=spring',
            '-Amapstruct.unmappedTargetPolicy=IGNORE'
    ]
}

tasks.named('test') {
    useJUnitPlatform()
}

def appDevConfig = file('src/main/resources/application-dev.yaml')
def devYaml = appDevConfig.exists() ? new Yaml().load(appDevConfig.text) : [:]
def devDatasource = devYaml?.spring?.datasource ? devYaml?.spring?.datasource as Map : [:]

flyway {
    url = devDatasource.url
    user = devDatasource.username
    password = devDatasource.password
}
